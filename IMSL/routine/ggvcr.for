C   IMSL ROUTINE NAME   - GGVCR
C
C-----------------------------------------------------------------------
C
C   LATEST REVISION     - NOVEMBER 1, 1984
C
C   PURPOSE             - GENERAL CONTINUOUS DISTRIBUTION RANDOM
C                           DEVIATE GENERATOR
C
C   USAGE               - CALL GGVCR (SUBRF,TBL,P1,P2,I3,I4,I5,IT,IOP,
C                                       DSEED,R,IER)
C
C   ARGUMENTS    SUBRF  - USER-SUPPLIED SUBROUTINE SUBPROGRAM WHICH
C                           PROVIDES THE VALUE OF THE DISTRIBUTION
C                           FUNCTION (D.F.) FOR EACH GIVEN VALUE OF
C                           THE RANDOM VARIABLE. THE D.F. VALUES MUST
C                           BE STRICTLY MONOTONE INCREASING. ONLY A
C                           DUMMY SUBROUTINE IS REQUIRED IF IOP(3) IS
C                           GREATER THAN OR EQUAL TO ZERO. THE USAGE
C                           IS AS FOLLOWS.
C                             CALL SUBRF(TBL,P1,P2,I3,I4,I5)
C
C                           SUBRF MUST APPEAR IN AN EXTERNAL SPECIFI-
C                           CATION STATEMENT IN THE CALLING PROGRAM.
C                TBL    - INPUT MATRIX OF DIMENSION (IOP(1)+4) BY 5
C                           CONTAINING POINTS ON THE D.F. FOR
C                           INTERPOLATION.
C                             TBL(I,1), WHERE I=3,4,...,IOP(1)+2,
C                             CONTAINS A STRICTLY MONOTONE INCREASING
C                             SEQUENCE OF RANDOM VARIABLE VALUES.
C                             TBL(I,2), WHERE I=3,4,...,IOP(1)+2,
C                             CONTAINS THE CORRESPONDING D.F. VALUES.
C                           IOP(3) LESS THAN ZERO IMPLIES THAT THE
C                             TBL(I,2), WHERE I=3,4,...,IOP(1)+2, ARE TO
C                             BE GENERATED BY USER ROUTINE SUBRF.  SEE
C                             REMARKS.
C                           THE REMAINING COMPONENTS OF TBL ARE WORK
C                             SPACE.
C                P1,P2, - PARAMETERS THAT MAY BE USED TO SEND DATA TO
C                I3,I4,     OR RETURN DATA FROM THE SUBROUTINE SUBRF.
C                I5         WHERE P1,P2 AND I3 ARE VECTORS. SEE REMARKS.
C                IT     - INPUT ROW DIMENSION OF MATRIX TBL EXACTLY AS
C                           SPECIFIED IN THE DIMENSION STATEMENT IN THE
C                           CALLING PROGRAM.
C                IOP    - INPUT/OUTPUT VECTOR OF LENGTH 3.
C                           IOP(1) ON INPUT IS THE NUMBER OF RANDOM
C                             VARIABLE VALUES FOR INTERPOLATING THE
C                             D.F.  IOP(1) MUST BE GREATER THAN 3.
C                           IOP(2) ON INPUT IS THE NUMBER OF RANDOM
C                             DEVIATES TO BE GENERATED.
C                           IOP(3) ON INPUT IS A FIRST CALL INDICATOR
C                             AND AN INDICATOR FOR GENERATION OF TBL.
C                             IOP(3) LESS THAN ZERO IMPLIES THIS IS THE
C                               FIRST CALL TO GGVCR  AND TBL(I,2), FOR
C                               J=3,4,...,IOP(1)+2, ARE GENERATED BY THE
C                               USER ROUTINE SUBRF.
C                             IOP(3) EQUAL TO ZERO IMPLIES THAT THIS IS
C                               THE FIRST CALL TO GGVCR AND COLUMN TWO
C                               OF TBL IS INPUT.  (SUBRF IS NOT USED.)
C                             IOP(3) GREATER THAN ZERO IMPLIES THIS IS
C                               NOT THE FIRST CALL TO GGVCR .
C                             IOP(3) IS OUTPUT AS 1.  HENCE, IF A CALL
C                               IS MADE TO GGVCR WITH NEW VALUES OF TBL
C                               OR WITH A NEW SUBRF, IOP(3) MUST BE
C                               RESET TO ZERO OR A NEGATIVE VALUE.
C                DSEED  - INPUT/OUTPUT DOUBLE PRECISION VARIABLE
C                           ASSIGNED AN INTEGER VALUE IN THE
C                           EXCLUSIVE RANGE (1.D0, 2147483647.D0).
C                           DSEED IS REPLACED BY A NEW VALUE TO BE
C                           USED IN A SUBSEQUENT CALL.
C                R      - OUTPUT VECTOR OF LENGTH IOP(2) CONTAINING THE
C                           DESIRED RANDOM DEVIATES.
C                IER    - ERROR PARAMETER. (OUTPUT)
C                         TERMINAL ERROR
C                           IER = 129 INDICATES THE NUMBER OF POINTS,
C                             IOP(1), FOR INTERPOLATING THE D.F. WAS
C                             SPECIFIED LESS THAN 4.
C                           IER = 130 INDICATES THAT EITHER THE RANDOM
C                             VARIABLE VALUES OR THE D.F. VALUES OR
C                             BOTH, IN TBL, ARE NOT STRICTLY MONOTONE
C                             INCREASING.
C                           IER = 131 INDICATES THE FIRST D.F. VALUE,
C                             TBL(3,2), IS NOT EQUAL TO ZERO OR
C                             THE LAST VALUE, TBL(IOP(1)+2,2),
C                             IS NOT EQUAL TO ONE.
C                           IER = 132 INDICATES THAT IT, INPUT ROW
C                             DIMENSION OF MATRIX TBL, WAS SPECIFIED
C                             LESS THAN IOP(1)+4.
C
C   REQD. IMSL ROUTINES - GGUBS,UERTST,UGETIO
C
C   NOTATION            - INFORMATION ON SPECIAL NOTATION AND
C                           CONVENTIONS IS AVAILABLE IN THE MANUAL
C                           INTRODUCTION OR THROUGH IMSL ROUTINE UHELP
C
C   REMARKS  1.  THE CALLING SEQUENCE PARAMETERS, P1,P2,I3,I4, AND I5
C                ARE NOT USED BY GGVCR. THESE ARE PARAMETERS WHICH THE
C                USER MAY REQUIRE TO RELAY DATA FROM (TO) HIS MAIN
C                PROGRAM TO (FROM) THE SUBROUTINE, SUBRF. P1 AND P2 HAVE
C                BEEN DIMENSIONED AS REAL VECTORS IN GGVCR AND I3 HAS
C                BEEN DIMENSIONED AS AN INTEGER VECTOR IN GGVCR TO ALLOW
C                THE USER TO DEFINE ANY OR ALL AS SUBSCRIPTED VARIABLES.
C                I4 AND I5 ARE INTEGER SCALARS. THESE PARAMETERS MAKE IT
C                POSSIBLE FOR THE USER TO DEFINE A SUBROUTINE OF SOME-
C                WHAT GENERAL APPLICABILITY WITHOUT THE USE OF COMMON.
C            2.  SUBRF MAY BE USED TO GENERATE COLUMN ONE OF TBL AS WELL
C                AS COLUMN TWO, BY USING GGVCR OPTIONS EXACTLY AS FOR
C                GENERATION OF COLUMN TWO ALONE, AND BY WRITING SUBRF
C                APPROPRIATELY.
C
C-----------------------------------------------------------------------
C
      SUBROUTINE GGVCR  (SUBRF,TBL,P1,P2,I3,I4,I5,IT,IOP,DSEED,R,IER)
C                                  SPECIFICATIONS FOR ARGUMENTS
      INTEGER            IOP(3),IT,IER,I3(1),I4,I5
      REAL               TBL(IT,1),R(1),P1(1),P2(1)
      DOUBLE PRECISION   DSEED
C                                  SPECIFICATIONS FOR LOCAL VARIABLES
      INTEGER            L,I,M,K,KL,KU,NX,N0,NXP2,ML,MU
      REAL               T1,T2,B,RM1,RM2,RM3,RM4,HALF,ONE,ZERO
      DATA               HALF/0.50/,ONE/1.0/,ZERO/0.0/
      DATA               EPS/Z3C100000/
C                                  TEST FOR NUMBER OF POINTS GREATER
C                                  THAN THREE.
C                                  FIRST EXECUTABLE STATEMENT
      IF (IOP(1).LT.4) GO TO 85
      IER = 0
      NX = IOP(1)
      NXP2 = NX+2
      IF (IT.LT.IOP(1)+4) GO TO 90
C                                  TEST FIRST CALL INDICATOR.
      IF (IOP(3)) 5,10,60
    5 CALL SUBRF (TBL,P1,P2,I3,I4,I5)
   10 DO 15 I=4,NXP2
         IF (TBL(I,2).GT.TBL(I-1,2)) GO TO 15
C                                  TERMINAL - DISTRIBUTION FUNCTION
C                                  NOT MONOTONE INCREASING
         GO TO 95
   15 CONTINUE
C                                  TEST FOR IER=131.
      DO 20 I=4,NXP2
         IF (TBL(I-1,1).GT.TBL(I,1)) GO TO 95
   20 CONTINUE
C                                  TEST FOR IER=132
      YZERO = ABS(TBL(3,2))
      YONE = ABS((TBL(NXP2,2)-ONE))
      IF (YZERO.LT.EPS.AND.YONE.LT.EPS) GO TO 25
      GO TO 100
C
   25 RM3 = (TBL(4,1)-TBL(3,1))/(TBL(4,2)-TBL(3,2))
      T1 = RM3-(TBL(4,1)-TBL(5,1))/(TBL(4,2)-TBL(5,2))
      RM2 = RM3+T1
      RM1 = RM2+T1
C                                  NOW GET THE SLOPES
      N0 = NX-2
      DO 50 I=3,NXP2
         IF (I.GT.N0) GO TO 30
         RM4 = (TBL(I+2,1)-TBL(I+1,1))/(TBL(I+2,2)-TBL(I+1,2))
         GO TO 35
   30    RM4 = RM3-RM2+RM3
   35    T1 = ABS(RM4-RM3)
         T2 = ABS(RM2-RM1)
         B = T1+T2
         IF (B.NE.ZERO) GO TO 40
C                                  IF DENOMINATOR IS ZERO, GET AVERAGE
         TBL(I,3) = HALF*(RM2+RM3)
         GO TO 45
   40    TBL(I,3) = (T1*RM2+T2*RM3)/B
   45    RM1 = RM2
         RM2 = RM3
         RM3 = RM4
   50 CONTINUE
      N0 = NX+1
C                                  COMPUTE THE COEFFICIENTS FOR THE
C                                    NX-1 INTERVALS
      DO 55 I=3,N0
         T1 = ONE/(TBL(I+1,2)-TBL(I,2))
         T2 = (TBL(I+1,1)-TBL(I,1))*T1
         B = (TBL(I,3)+TBL(I+1,3)-T2-T2)*T1
         TBL(I,5) = B*T1
         TBL(I,4) = -B+(T2-TBL(I,3))*T1
   55 CONTINUE
      IF (IER.GT.127) GO TO 9000
C                                  RESET FIRST CALL INDICATOR.
      IOP(3) = 1
   60 L = IOP(2)
C                                  GET IOP(2) RANDOM DEVIATES.
      CALL GGUBS (DSEED,IOP(2),R)
      MU = IOP(1)+2
      ML = 3
      M = (MU+ML)/2
C                                  BISECT TBL SUCH THAT EACH R VALUE
C                                  LIES BETWEEN TBL(K,2) AND TBL(K+1,2).
C                                  THEN COMPUTE NEW R VALUE.
      DO 80 I=1,L
         K = M
         KU = MU
         KL = ML
   65    IF (R(I).GE.TBL(K,2).AND.R(I).LE.TBL(K+1,2)) GO TO 75
         IF (R(I).GT.TBL(K+1,2)) GO TO 70
         KU = K
         K = (KU+KL)/2
         GO TO 65
   70    KL = K
         K = (KU+KL)/2
         K = MIN0(K,MU)
         GO TO 65
   75    DX = R(I)-TBL(K,2)
         R(I) = TBL(K,1)+TBL(K,3)*(DX)+TBL(K,4)*(DX)
     1   **2+TBL(K,5)*(DX)**3
   80 CONTINUE
      IF (IER) 9000,9005,9000
   85 IER = 129
      GO TO 9000
   90 IER = 132
      GO TO 9000
   95 IER = 130
      GO TO 9000
  100 IER = 131
 9000 CONTINUE
      CALL UERTST (IER,'GGVCR ')
 9005 RETURN
      END
